*
   GIT.FILENAME = 'EVENT'
   GIT.REPO = 'https://github.com/Krowemoh/TCL-Utilities.git'
*
* VERSION
*
   VERSION = '1'
*
   @USER1 = 'EVENT'
   @USER2 = 'EVENT'
*
   EQU TRUE TO 1
   EQU FALSE TO 0
*
   EQU EVENT.TO.ATTRIBUTE TO 1
   EQU EVENT.FROM.ATTRIBUTE TO 2
   EQU EVENT.SUBJECT.ATTRIBUTE TO 3
   EQU EVENT.BODY.ATTRIBUTE TO 4
*
   EQU EVENT.START.DATE.ATTRIBUTE TO 5
   EQU EVENT.START.TIME.ATTRIBUTE TO 6
   EQU EVENT.START.TZ.ATTRIBUTE TO 7
*
   EQU EVENT.END.DATE.ATTRIBUTE TO 8
   EQU EVENT.END.TIME.ATTRIBUTE TO 9
   EQU EVENT.END.TZ.ATTRIBUTE TO 10
*
   DIM EVENT.ITEM(10)
   MAT EVENT.ITEM = ''
*
   OPEN '','EVENT-FILE' TO EVENT.FILE ELSE
      PRINT 'Unable to open file: EVENT-FILE'
      STOP
   END
*
   OPEN '','CONTROL-FILE' TO CONTROL.FILE ELSE
      PRINT 'Unable to open file: CONTROL-FILE'
      STOP
   END
*
   CALL GET.ARGUMENTS(ARGS)
*
   ARGS.LEN = DCOUNT(ARGS,@AM)
*
   IF ARGS.LEN = 1 THEN
      PRINT 'EVENT - Create an Event'
      PRINT
      PRINT '    EVENT --SUBJECT "Day Event" --BODY "Something silly." --START-DATE "01JAN2024"'
      PRINT '    EVENT --SUBJECT "Day Timed Event" --BODY "Something silly." --START-DATE "01JAN2024" -- START-TIME "13:00" --END-TIME "14:00"'
      PRINT '    EVENT --SUBJECT "Overnight Event" --BODY "Something silly." --START-DATE "01JAN2024" --START-TIME "13:52" --END-DATE "02JAN2024" --END-TIME "9:00"'
      PRINT
      STOP
*
   END
*
   DEFAULT.FROM = 'Nivethan'
   DEFAULT.TO = DEFAULT.FROM
*
   FROM.ADDRESS = ''
   TO.ADDRESS = ''
*
   SUBJECT = ''
   BODY = ''
*
   START.DATE = ''
   START.TIME = ''
   START.TZ = ''
*
   END.DATE = ''
   END.TIME = ''
   END.TZ = ''
*
   LOCATE('--TO',ARGS;FLAG.POS) THEN
      TO.ADDRESS = ARGS<FLAG.POS+1>
      ARGS = DELETE(ARGS,FLAG.POS)
      ARGS = DELETE(ARGS,FLAG.POS)
   END ELSE
      TO.ADDRESS = DEFAULT.TO
   END
*
   LOCATE('--FROM',ARGS;FLAG.POS) THEN
      FROM.ADDRESS = ARGS<FLAG.POS+1>
      ARGS = DELETE(ARGS,FLAG.POS)
      ARGS = DELETE(ARGS,FLAG.POS)
   END ELSE
      FROM.ADDRESS = DEFAULT.FROM
   END
*
   LOCATE('--SUBJECT',ARGS;FLAG.POS) THEN
      SUBJECT = ARGS<FLAG.POS+1>
      ARGS = DELETE(ARGS,FLAG.POS)
      ARGS = DELETE(ARGS,FLAG.POS)
   END ELSE
      PRINT 'Cannot have an Event without a subject.'
      STOP
   END
*
   LOCATE('--BODY',ARGS;FLAG.POS) THEN
      BODY = ARGS<FLAG.POS+1>
      ARGS = DELETE(ARGS,FLAG.POS)
      ARGS = DELETE(ARGS,FLAG.POS)
   END
*
   LOCATE('--START-DATE',ARGS;FLAG.POS) THEN
      START.DATE = ARGS<FLAG.POS+1>
*
      START.DATE = ICONV(START.DATE,'D')
*
      ARGS = DELETE(ARGS,FLAG.POS)
      ARGS = DELETE(ARGS,FLAG.POS)
   END ELSE
      PRINT 'Cannot have an Event without a start date.'
      STOP
   END
*
   LOCATE('--START-TIME',ARGS;FLAG.POS) THEN
      START.TIME = ARGS<FLAG.POS+1>
*
      START.TIME = ICONV(START.TIME,'MT')
*
      ARGS = DELETE(ARGS,FLAG.POS)
      ARGS = DELETE(ARGS,FLAG.POS)
   END ELSE
      START.TIME = 0
   END
*
   LOCATE('--START-TZ',ARGS;FLAG.POS) THEN
      START.TZ = ARGS<FLAG.POS+1>
      ARGS = DELETE(ARGS,FLAG.POS)
      ARGS = DELETE(ARGS,FLAG.POS)
   END ELSE
      START.TZ = 'America/New_York'
   END
*
   LOCATE('--END-DATE',ARGS;FLAG.POS) THEN
      END.DATE = ARGS<FLAG.POS+1>
*
      END.DATE = ICONV(END.DATE,'D')
*
      ARGS = DELETE(ARGS,FLAG.POS)
      ARGS = DELETE(ARGS,FLAG.POS)
   END ELSE
      END.DATE = START.DATE
   END
*
   LOCATE('--END-TIME',ARGS;FLAG.POS) THEN
      END.TIME = ARGS<FLAG.POS+1>
*
      END.TIME = ICONV(END.TIME,'MT')
*
      ARGS = DELETE(ARGS,FLAG.POS)
      ARGS = DELETE(ARGS,FLAG.POS)
   END ELSE
      END.TIME = ICONV('23:59','MT')
   END
*
   LOCATE('--END-TZ',ARGS;FLAG.POS) THEN
      END.TZ = ARGS<FLAG.POS+1>
      ARGS = DELETE(ARGS,FLAG.POS)
      ARGS = DELETE(ARGS,FLAG.POS)
   END ELSE
      END.TZ = 'America/New_York'
   END
*
   PRINT 'Subject: ' : SUBJECT
   PRINT 'Body: ' : BODY
   PRINT 'Duration: ' : OCONV(START.TIME,'MT') : ' ' : OCONV(START.DATE,'D') : ' to ' : OCONV(END.TIME,'MT') : ' ' : OCONV(END.DATE,'D')
*
   MAT EVENT.ITEM = ''
*
   EVENT.ITEM(EVENT.TO.ATTRIBUTE) = TO.ADDRESS
   EVENT.ITEM(EVENT.FROM.ATTRIBUTE) = FROM.ADDRESS
*
   EVENT.ITEM(EVENT.SUBJECT.ATTRIBUTE) = SUBJECT
   EVENT.ITEM(EVENT.BODY.ATTRIBUTE) = BODY
*
   EVENT.ITEM(EVENT.START.DATE.ATTRIBUTE) = START.DATE
   EVENT.ITEM(EVENT.START.TIME.ATTRIBUTE) = START.TIME
   EVENT.ITEM(EVENT.START.TZ.ATTRIBUTE) = START.TZ
*
   EVENT.ITEM(EVENT.END.DATE.ATTRIBUTE) = END.DATE
   EVENT.ITEM(EVENT.END.TIME.ATTRIBUTE) = END.TIME
   EVENT.ITEM(EVENT.END.TZ.ATTRIBUTE) = END.TZ
*
   CALL GET.NEXT.ID(ITEM.ID,EVENT.FILE)
*
   MATWRITE EVENT.ITEM ON EVENT.FILE,ITEM.ID
*
   PRINT 'Added to calendar: ' : ITEM.ID
*
* END OF PROGRAM
*
   END
*
