   SUBROUTINE SOLR.REMOVE(STATUS.MATRIX,COLLECTION.NAME,ID.LIST)
*
   GIT.FILENAME = 'SOLR.REMOVE'
   GIT.REPO = 'https://github.com/Krowemoh/TCL-Utilities.git'
*
* COMPILER DIRECTIVES
*
   $DEFINE DATABASE.QM
   $DEFINE PLATFORM.LINUX
*
   $IFDEF DATABASE.QM
      $CATALOGUE LOCAL
   $ENDIF
*
* VERSION
*
   VERSION = '1'
*
   @USER1 = 'SOLR.REMVOE'
   @USER2 = 'SOLR.REMOVE'
*
   EQU TRUE TO 1
   EQU FALSE TO 0
*
   STATUS.MATRIX = TRUE
*
   TMP.PATH = '/tmp'
*
   OPENPATH TMP.PATH TO TMP.FILE ELSE
      STATUS.MATRIX<1> = FALSE
      STATUS.MATRIX<2> = 'Failed to open: ' : TMP.PATH
      STATUS.MATRIX<3> = DATE()
      STATUS.MATRIX<4> = TIME()
      RETURN
   END
*
   $IFDEF DATABASE.UV
      EXECUTE 'SH -c "mktemp"' CAPTURING TMP.FILE.PATH
   $ENDIF
*
   $IFDEF DATABASE.QM
      EXECUTE '!mktemp' CAPTURING TMP.FILE.PATH
   $ENDIF
*
   ID.ARRAY = ID.LIST
*
   CALL JSON.CREATE.ARRAY(ID.ARRAY)
*
   JSON.DATA = ''
   CALL JSON.ADD(JSON.DATA,'delete',ID.ARRAY)
   CALL JSON.CREATE.OBJECT(JSON.DATA)
*
   TMP.ID = FIELD(TMP.FILE.PATH,'/',3)
   WRITE JSON.DATA ON TMP.FILE, TMP.ID
*
   CMD = 'curl --silent'
   CMD = CMD : ' -X POST'
   CMD = CMD : " -H 'Content-Type: application/json'"
   CMD = CMD : ' http://localhost:8983/solr/' : COLLECTION.NAME : '/update'
   CMD = CMD : ' --data-binary @' : TMP.FILE.PATH
   CMD = CMD : ' | jq -c .'
*
   $IFDEF DATABASE.UV
      EXECUTE \SH -c "\ : CMD : \"\ CAPTURING RESULTS
   $ENDIF
*
   $IFDEF DATABASE.QM
      EXECUTE '!' : CMD CAPTURING RESULTS
   $ENDIF
*
   CALL JSON.PARSE(RESULTS,RESPONSE.JSON)
   CALL JSON.QUERY(RESPONSE.JSON,'.responseHeader.status',STATUS)
*
   IF STATUS = '0' THEN
      STATUS.MATRIX = TRUE
*
   END ELSE
      STATUS.MATRIX<1> = FALSE
      STATUS.MATRIX<2> = RESULTS
      STATUS.MATRIX<3> = DATE()
      STATUS.MATRIX<4> = TIME()
   END
*
   DELETE TMP.FILE, TMP.ID
*
   RETURN
*
* END OF PROGRAM
*
   END
*
