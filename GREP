*
    GIT.FILENAME = 'GREP'
    GIT.REPO = 'https://github.com/Krowemoh/TCL-Utilities.git'
*
* VERSION
*
    VERSION = '1'
*
    @USER1 = 'GREP'
    @USER2 = 'GREP'
*
    EQU TRUE TO 1
    EQU FALSE TO 0
*
    CALL GET.ARGUMENTS(ARGUMENTS)
*
    DICT = ''
*
    IF ARGUMENTS<2> = 'DICT' THEN
        ARGUMENTS = DELETE(ARGUMENTS,2)
        DICT = 'DICT'
    END
*
    ARGS.LEN = DCOUNT(ARGUMENTS,@AM)
*
    IF ARGS.LEN = 1 THEN
        PRINT 'GREP - Regular Expression Search'
        PRINT
        PRINT 'GREP [DICT] {FILE} {RECORD.ID} {REGEX}'
        PRINT
        STOP
    END
*
    IF ARGS.LEN = 2 THEN
        PRINT 'Invalid number of args.'
        STOP
    END
*
    FILE.NAME = ARGUMENTS<2>
*
    OPEN DICT,FILE.NAME TO FILE ELSE
        PRINT 'Unable to open file: ' : FILE.NAME
        STOP
    END
*
    IF ARGS.LEN = 3 THEN
        REGULAR.EXP = ARGUMENTS<3>
        SELECT FILE
*
    END ELSE IF ARGS.LEN = 4 THEN
        RECORD.ID = ARGUMENTS<3>
        REGULAR.EXP = ARGUMENTS<4>
        SELECT RECORD.ID
    END
*
    IF REGULAR.EXP[1,1] = '"' AND REGULAR.EXP[LEN(REGULAR.EXP),1] = '"' THEN
        REGULAR.EXP = REGULAR.EXP[2,LEN(REGULAR.EXP)-2]
    END
*
    DIM CONTENT(10000)
    MAT CONTENT = ''
*
    LOOP
        READNEXT ITEM.ID ELSE ITEM.ID = ''
*
    UNTIL ITEM.ID = '' DO
        MATREAD CONTENT FROM FILE,ITEM.ID ELSE MAT CONTENT = ''
        NUMBER.OF.LINES = INMAT()
*
        FOR LINE.CTR = 1 TO NUMBER.OF.LINES
*
            TEXT = CONTENT(LINE.CTR)
            REGEXP = REGULAR.EXP
*
            GOSUB MATCH
*
            IF RESULT THEN
                PRINT ITEM.ID : ': ' : LINE.CTR : ': ' : TEXT
            END
        NEXT LINE.CTR
    REPEAT
*
    STOP
*
*********************  S U B R O U T I N E  *********************
*
MATCH:NULL
*
    BREAK.IMMEDIATE = FALSE
    RESULT = FALSE
*
    T.LEN = LEN(TEXT)
    R.LEN = LEN(REGEXP)
*
    I = 0
*
    IF REGEXP = '$' THEN
        RESULT = TRUE
        RETURN
    END
*
    IF REGEXP[1,1] = '^' THEN
        BREAK.IMMEDIATE = TRUE
        REGEXP = REGEXP[2,999]
        R.LEN = LEN(REGEXP)
    END
*
    LOOP
*
        I = I + 1
*
        IF BREAK.IMMEDIATE AND I # 1 THEN
            EXIT
        END
*
        R.CTR = 1
        T.CTR = I
*
        STAR.MATCH = ''
*
        LOOP
            T = TEXT[T.CTR,1]
*
            IF R.CTR = 1 THEN
                P.R = ''
            END ELSE
                P.R = REGEXP[R.CTR-1,1]
            END
*
            R = REGEXP[R.CTR,1]
*
            IF R.CTR = R.LEN THEN
                N.R = ''
            END ELSE
                N.R = REGEXP[R.CTR+1,1]
            END
*
            IF N.R = '*' THEN
                R.CTR = R.CTR + 1
                CONTINUE
*
            END ELSE IF R = '*' THEN
*
                IF P.R = '' THEN
                    EXIT
                END
*
                DONE = FALSE
                LOOP
                    IF (T = P.R OR P.R = '.') AND T # N.R THEN
                        T.CTR = T.CTR + 1
                        T = TEXT[T.CTR,1]
                    END ELSE
                        DONE = TRUE
                    END
                UNTIL DONE OR T.CTR > T.LEN DO REPEAT
*
                IF T.CTR > T.LEN AND N.R # '' AND N.R # '$' THEN
                    EXIT
                END
*
                R.CTR = R.CTR + 1
*
            END ELSE IF R = '$' THEN
                IF T.CTR > T.LEN THEN
                    RESULT = TRUE
                    RETURN
                END ELSE
                    RESULT = FALSE
                    RETURN
                END
*
            END ELSE IF T = R OR (R = '.' AND T # '') THEN
                T.CTR = T.CTR + 1
                R.CTR = R.CTR + 1
*
                IF N.R = '$' THEN
                    IF T.CTR > T.LEN THEN
                        RESULT = TRUE
                        RETURN
                    END ELSE
                        EXIT
                    END
                END
*
            END ELSE IF R.CTR > R.LEN THEN
                RESULT = TRUE
                RETURN
*
            END ELSE
                EXIT
            END
*
            IF R.CTR > R.LEN THEN
                RESULT = TRUE
                RETURN
            END
*
        UNTIL R.CTR > R.LEN DO REPEAT
*
    UNTIL I > T.LEN DO REPEAT
*
    RETURN
*
* END OF PROGRAM
*
    END
*
