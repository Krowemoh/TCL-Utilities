    SUBROUTINE JSON.CREATE.OBJECT(BUFFER)
*
    GIT.FILENAME = 'JSON.CREATE.OBJECT'
    GIT.REPO = 'https://github.com/Krowemoh/TCL-Utilities.git'
*
* VERSION
*
    VERSION = '1'
*
    @USER1 = 'JSON.CREATE.OBJECT'
    @USER2 = 'JSON.CREATE.OBJECT'
*
    NUMBER.OF.KEYS = DCOUNT(BUFFER,@AM)
*
    KEYS = ''
    JSON = ''
*
    FOR I = 1 TO NUMBER.OF.KEYS
        KEY = BUFFER<I,1>
        VALUE = BUFFER<I,2>
*
        LOCATE(KEY,KEYS;KEY.POS) ELSE
            KEYS<-1> = KEY
            KEY.POS = -1
        END
*
        IF VALUE = '' THEN
            GOSUB QUOTE.VALUE
            JSON<KEY.POS> = KEY.VALUE
*
        END ELSE IF VALUE[1,1] = '0' THEN
            GOSUB QUOTE.VALUE
            JSON<KEY.POS> = KEY.VALUE
*
        END ELSE IF VALUE[1,1] = '.' THEN
            GOSUB QUOTE.VALUE
            JSON<KEY.POS> = KEY.VALUE
*
        END ELSE IF (VALUE[1,1] = '[' OR VALUE[1,1] = '{' OR NUM(VALUE)) THEN
            JSON<KEY.POS> = '"' : KEY : '": ' : VALUE
*
        END ELSE
            GOSUB QUOTE.VALUE
            JSON<KEY.POS> = KEY.VALUE
        END
    NEXT I
*
    CONVERT @AM TO ',' IN JSON
*
    JSON = '{ ' : JSON : ' }'
*
    BUFFER = JSON
*
    RETURN
*
*********************  S U B R O U T I N E  *********************
*
QUOTE.VALUE:NULL
*
    KEY = CHANGE(KEY,'\','\\')
    KEY = CHANGE(KEY,'"','\"')
    KEY = CHANGE(KEY,'/','\/')
    KEY = CHANGE(KEY,CHAR(0),'\u0000')
*
    VALUE = CHANGE(VALUE,'\','\\')
    VALUE = CHANGE(VALUE,'"','\"')
    VALUE = CHANGE(VALUE,'/','\/')
    VALUE = CHANGE(VALUE,CHAR(0),'\u0000')
*
    KEY.VALUE = '"' : KEY : '": "' : VALUE : '"'
*
    RETURN
*
* END OF PROGRAM
*
    END
*


