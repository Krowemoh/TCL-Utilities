   SUBROUTINE GET.NEXT.ID(NEXT.ID,SEQUENCE.ID,FILE)
*
   GIT.FILENAME = 'GET.NEXT.ID'
   GIT.REPO = 'https://github.com/Krowemoh/TCL-Utilities.git'
*
* VERSION
*
   VERSION = '1'
*
   @USER1 = 'GET.NEXT.ID'
   @USER2 = 'GET.NEXT.ID'
*
   EQU TRUE TO 1
   EQU FALSE TO 0
*
* CHECK CACHE FOR ID
*
   OPEN '','CONTROL-FILE' TO CONTROL.FILE ELSE
      PRINT 'Unable to open file: CONTROL-FILE'
      STOP
   END
*
* MAKE SURE CACHE CONTAINS THE SEQUENCE.ID
* MAKE SURE PREVIOUS ID IS USED
* MAKE SURE CACHE.ID IS NOT ALREADY USED
*
   READV POSSIBLE.ID FROM CONTROL.FILE,SEQUENCE.ID,1 THEN
      READV ANYTHING FROM FILE,POSSIBLE.ID-1,1 THEN
         READV ANYTHING FROM FILE,POSSIBLE.ID,1 ELSE
            NEXT.ID = POSSIBLE.ID
            WRITEV POSSIBLE.ID+1 ON CONTROL.FILE,SEQUENCE.ID,1
            RETURN
         END
      END
   END
*
* SAVE ORIGINAL.LIST IF ACTIVE LIST EXISTS
*
   SAVED.LIST = TRUE
   READLIST ORIGINAL.LIST ELSE SAVED.LIST = FALSE
*
* LOOP THROUGH IDS TO FIND HIGHEST NUMBER
*
   NEXT.ID = 0
   DONE = FALSE
   SELECT FILE
*
   LOOP
      READNEXT ITEM.ID ELSE DONE = TRUE
*
   UNTIL DONE DO
      IF NOT(NUM(ITEM.ID)) THEN
         CONTINUE
      END
*
      IF ITEM.ID > NEXT.ID THEN
         NEXT.ID = ITEM.ID
      END
   REPEAT
*
   NEXT.ID = NEXT.ID + 1
   WRITEV NEXT.ID+1 ON CONTROL.FILE,SEQUENCE.ID,1
*
* RESTORE ORIGINAL.LIST IF LIST WAS SAVED
*
   IF SAVED.LIST THEN
      SELECT ORIGINAL.LIST
   END
*
   RETURN
*
* END OF PROGRAM
*
   END
*
