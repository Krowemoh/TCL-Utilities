   SUBROUTINE SERAPHIM.PARSE.ROUTES(REQUEST.URL,ROUTES,ROUTE.POS,MAT REQUEST)
*
   GIT.FILENAME = 'SERAPHIM.PARSE.ROUTES'
   GIT.REPO = 'https://github.com/Krowemoh/TCL-Utilities.git'
*
   $DEFINE DATABASE.QM
*
   $IFDEF DATABASE.QM
      $CATALOGUE LOCAL
   $ENDIF
*
   $INCLUDE SERAPHIM.HEADER
*
   REQUEST.TYPE = REQUEST(REQUEST.TYPE.ATTRIBUTE)
*
   NUMBER.OF.ROUTES = DCOUNT(ROUTES<1>,@VM)
   ROUTE.POS = ''
*
   FOR ROUTE.CTR = 1 TO NUMBER.OF.ROUTES
      TYPE = FIELD(ROUTES<1,ROUTE.CTR>,' ',1)
*
      IF TYPE # REQUEST.TYPE THEN
         REQUEST(REQUEST.SLUGS.ATTRIBUTE)<1> = ''
         REQUEST(REQUEST.SLUGS.ATTRIBUTE)<2> = ''
         CONTINUE
      END
*
      ROUTE = FIELD(ROUTES<1,ROUTE.CTR>,' ',2)
*
      ROUTE.PATTERN = ''
      SLUG.PATTERN = "0X'<'0X'>'0X"
      SLUG.CTR = 0
*
      LOOP UNTIL NOT(ROUTE MATCHES SLUG.PATTERN) DO
         ONE = MATCHFIELD(ROUTE,SLUG.PATTERN,1)
         SLUG = MATCHFIELD(ROUTE,SLUG.PATTERN,3)
         THREE = MATCHFIELD(ROUTE,SLUG.PATTERN,5)
*
         ROUTE = "'" : ONE : "'0X'" : THREE : "'"
*
         IF REQUEST.URL MATCHES ROUTE THEN
            SLUG.CTR = SLUG.CTR + 2
*
            REQUEST(REQUEST.SLUGS.ATTRIBUTE)<1,-1> = SLUG
*
            SLUG.VALUE = MATCHFIELD(REQUEST.URL,ROUTE,SLUG.CTR)
            REQUEST(REQUEST.SLUGS.ATTRIBUTE)<2,-1> = SLUG.VALUE
         END
      REPEAT
*
      ROUTE.PATTERN = ROUTE
*
      IF REQUEST.URL MATCHES ROUTE.PATTERN THEN
         ROUTE.POS = ROUTE.CTR
         EXIT
*
      END ELSE
         REQUEST(REQUEST.SLUGS.ATTRIBUTE)<1> = ''
         REQUEST(REQUEST.SLUGS.ATTRIBUTE)<2> = ''
      END
   NEXT ROUTE.CTR
*
   RETURN
*
* END OF PROGRAM
*
   END
*
