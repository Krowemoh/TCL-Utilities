   SUBROUTINE FILE.TRANSFER.PROTOCOL.SECURE(RESULTS,SERVER,USERNAME,PASSWORD,LOCAL.PATH,REMOTE.PATH,DIRECTION)
*
   GIT.FILENAME = 'FILE.TRANSFER.PROTOCOL.SECURE'
   GIT.REPO = 'https://github.com/Krowemoh/TCL-Utilities.git'
*
* VERSION
*
   VERSION = '1'
*
   @USER1 = 'FILE.TRANSFER.PROTOCOL.SECURE'
   @USER2 = 'FILE.TRANSFER.PROTOCOL.SECURE'
*
   EQU TRUE TO 1
   EQU FALSE TO 0
*
   IF DIRECTION # 'SEND-FILE' AND DIRECTION # 'GET-FILE' THEN
      PRINT 'Invalid direction.'
      RETURN
   END
*
   PORT = ''
*
   IF INDEX(SERVER,':',1) # 0 THEN
      PORT = SERVER[INDEX(SERVER,':',1)+1,999]
      SERVER = SERVER[1,INDEX(SERVER,':',1)-1]
   END
*
   COMMAND = 'lftp <<EOF'
*
   IF PORT THEN
      COMMAND = COMMAND : CHAR(10) : 'open -p ' : PORT : ' ' : SERVER
   END ELSE
      COMMAND = COMMAND : CHAR(10) : 'open ' : SERVER
   END
*
   COMMAND = COMMAND : CHAR(10) : 'user ' : USERNAME : ' ': PASSWORD
*
   COMMAND = COMMAND : CHAR(10) : 'set xfer:clobber on'
*
   IF DIRECTION = 'SEND-FILE' THEN
      COMMAND = COMMAND : CHAR(10) : 'put ' : LOCAL.PATH : ' -o ' : REMOTE.PATH
*
   END ELSE IF DIRECTION = 'GET-FILE' THEN
      COMMAND = COMMAND : CHAR(10) : 'get ' : REMOTE.PATH : ' -o ' : LOCAL.PATH
   END
*
   COMMAND = COMMAND : CHAR(10) : 'quit'
   COMMAND = COMMAND : CHAR(10) : 'EOF'
*
   SHELL.COMMAND = 'SH -c "' : COMMAND : '"'
   EXECUTE SHELL.COMMAND CAPTURING RESULTS
*
   RETURN
*
* END OF PROGRAM
*
   END
*
   
