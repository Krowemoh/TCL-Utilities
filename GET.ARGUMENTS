    SUBROUTINE GET.ARGUMENTS(ARGS)
*
    GIT.FILENAME = 'GET.ARGUMENTS'
    GIT.REPO = 'https://github.com/Krowemoh/TCL-Utilities.git'
*
    COMMAND = SENTENCE()
*
    ARGS = ''
    TOKEN = ''
*
    FOR LETTER.CTR = 1 TO LEN(COMMAND)
        CHARACTER = COMMAND[LETTER.CTR,1]
        IF CHARACTER = ' ' THEN
            IF TOKEN # '' THEN ARGS<-1> = TOKEN
            TOKEN = ''
*
        END ELSE IF CHARACTER = '"' THEN
            IF TOKEN # '' THEN ARGS<-1> = TOKEN
            TOKEN = ''
*
            LOOP
                LETTER.CTR = LETTER.CTR + 1
                CHARACTER = COMMAND[LETTER.CTR,1]
            UNTIL CHARACTER = '"' OR LETTER.CTR > LEN(COMMAND) DO
                TOKEN = TOKEN : CHARACTER
            REPEAT
*
            IF TOKEN # '' THEN ARGS<-1> = TOKEN
            TOKEN = ''
*
        END ELSE
            TOKEN = TOKEN : CHARACTER
        END
    NEXT LETTER.CTR
*
    IF TOKEN # '' THEN ARGS<-1> = TOKEN
*
    RETURN
*
* END OF PROGRAM
*
    END
*

